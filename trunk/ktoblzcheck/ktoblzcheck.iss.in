; Script generated by the Inno Setup Script Wizard and heavily
; modified by hand by Christian Stimming.
;
; Inno Setup Compiler: See http://www.jrsoftware.org/isdl.php
;

[Setup]
; Using the name here directly because we want it capitalized
AppName=KtoBLZCheck
AppVerName=Ktoblzcheck @VERSION@
AppPublisher=Ktoblzcheck Development Team
AppPublisherURL=http://sourceforge.net/projects/ktoblzcheck
AppSupportURL=http://sourceforge.net/support/getsupport.php?group_id=60375
AppUpdatesURL=http://sourceforge.net/project/showfiles.php?group_id=60375
DefaultDirName={reg:HKLM\Software\Ktoblzcheck\Paths,prefix|{pf}\@PACKAGE@}
LicenseFile=COPYING
Compression=lzma
SolidCompression=yes
OutputDir=.
OutputBaseFilename=@PACKAGE@-@VERSION@-setup
UninstallFilesDir={app}\uninstall
InfoAfterFile=README

[Types]
Name: "full"; Description: "{cm:FullInstall}"
Name: "binary"; Description: "{cm:BinaryInstall}"
Name: "custom"; Description: "{cm:CustomInstall}"; Flags: iscustom

[Components]
Name: "main"; Description: "{cm:MainFiles}"; Types: full binary custom; Flags: fixed
Name: "devel"; Description: "{cm:DevelFiles}"; Types: full
Name: "doc"; Description: "{cm:DocFiles}"; Types: full

[Files]
; The main DLL
Source: "src\lib\ktoblzcheck32.dll"; DestDir: "{win}"; Flags: sharedfile; Components: main
Source: "win32-tmp\bin\ktoblzcheck.exe"; DestDir: "{app}"; Components: main

; The data files
Source: "win32-tmp\data\*"; DestDir: "{app}\data"; Flags: ignoreversion; Components: main

; The lib file for linking against it
Source: "src\lib\ktoblzcheck32.lib"; DestDir: "{app}\lib"; Flags: sharedfile; Components: devel

; The headers. 
Source: "win32-tmp\include\*"; DestDir: "{app}\include"; Flags: ignoreversion recursesubdirs; Components: devel
; Source: "win32-tmp\share\*"; DestDir: "{app}\share"; Flags: ignoreversion recursesubdirs; Components: devel

; And all the documentation
Source: "README"; DestDir: "{app}\doc"; Components: main
Source: "COPYING"; DestDir: "{app}\doc"; Flags: ignoreversion; Components: main
Source: "README.WIN32"; DestDir: "{app}\doc"; Components: doc
Source: "AUTHORS"; DestDir: "{app}\doc"; Components: doc
Source: "ChangeLog"; DestDir: "{app}\doc"; Components: doc


; Define the registry keys Setup should create (HKLM = HKEY_LOCAL_MACHINE)
[Registry]
Root: HKLM; Subkey: "Software\Ktoblzcheck\Paths"; ValueType: string; ValueName: "prefix"; ValueData: "{app}"
Root: HKLM; Subkey: "Software\Ktoblzcheck\Paths"; ValueType: string; ValueName: "datadir"; ValueData: "{app}\data"


[Languages]
; Now all the languages available in the choicebox at the beginning
Name: "en"; MessagesFile: "compiler:Default.isl"
Name: "de"; MessagesFile: "compiler:Languages\German.isl"

; Modify wizard messages here, if desired.
; [Messages]

; The customized messages with their translations here.
[CustomMessages]
; *** "Select Components" wizard page
FullInstall=Full installation
BinaryInstall=Install only binaries
CustomInstall=Custom installation

MainFiles=Ktoblzcheck executable, bank data and DLL Library
DevelFiles=Development files
DocFiles=Documentation files

de.FullInstall=Komplett-Installation
de.BinaryInstall=Nur Binärdateien installieren
de.CustomInstall=Benutzerdefiniert

de.MainFiles=Ktoblzcheck-Programm, Bankenliste und DLL Bibliothek
de.DevelFiles=Entwickler-Dateien
de.DocFiles=Dokumentations-Dateien
